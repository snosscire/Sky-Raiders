namespace modifies Network {
	number MESSAGE_DISCONNECT_ME   = 155;
	number MESSAGE_PLAYER_UPDATE   = 154;
	number MESSAGE_NEW_PLAYER      = 153;
	number MESSAGE_INITIALIZE_JOIN = 152;
	number MESSAGE_PLAYER_HIT      = 151;
	number MESSAGE_ROUND_END       = 150;
	
	object client;
	function currentClient() {
		return .client;
	}
	function connect( string host, number port, string name ) {
		.client = Network.Client.start(host, port);
		if( .client )
			return true;
		return false;
	}
	function disconnect() {
		if( .client ) {
			.client.destroy();
			.client = null;
			MessageHandler.invokeListeners(new Network.DisconnectMeMessage());
		}
	}
	
	function addByteToData( string& data, number value ) {
		data += String.numberToByte((value & 0xFF));
	}
	function addShortToData( string& data, number value ) {
		data += String.numberToByte((value & 0xFF));
		data += String.numberToByte((value >> 8) & 0xFF);
	}
	function addLongToData( string& data, number value ) {
		data += String.numberToByte((value & 0xFF));
		data += String.numberToByte((value >> 8) & 0xFF);
		data += String.numberToByte((value >> 16) & 0xFF);
		data += String.numberToByte((value >> 24) & 0xFF);
	}
	function addDoubleToData( string& data, number value ) {
		.addLongToData(data, (value * 10000.0).floor().round());
	}
	function addStringToData( string& data, string value ) {
		.addLongToData(data, value.length());
		data += value;
	}
	function readByteFromData( string& data ) {
		number value = 0;
		value |= data[0].byteToNumber();
		if( data.length() > 1 )
			data = data[1..];
		return value;
	}
	function readShortFromData( string& data ) {
		number value = 0;
		value |= data[0].byteToNumber();
		value |= data[1].byteToNumber() << 8;
		if( data.length() > 2 )
			data = data[2..];
		return value;
	}
	function readLongFromData( string& data ) {
		number value = 0;
		value |= data[0].byteToNumber();
		value |= data[1].byteToNumber() << 8;
		value |= data[2].byteToNumber() << 16;
		value |= data[3].byteToNumber() << 24;
		if( data.length() > 4 )
			data = data[4..];
		return value;
	}
	function readDoubleFromData( string& data ) {
		number value = .readLongFromData(data);
		return value / 10000.0;
	}
	function readStringFromData( string& data ) {
		number length = .readLongFromData(data);
		string value = data[..(length - 1)];
		if( data.length() > length )
			data = data[length..];
		return value;
	}
	
	function send( object message ) {
		.client.send(message.toData());
	}
	
	function createDataMessage( string& data ) {
		object message;
		number type = Network.readByteFromData(data);
		switch( type ) {
			case Network.MESSAGE_NEW_PLAYER:    message = Network.NewPlayerMessage.fromData(data, false); break;
			case Network.MESSAGE_PLAYER_UPDATE: message = Network.PlayerUpdateMessage.fromData(data, false); break;
			case Network.MESSAGE_PLAYER_HIT:    message = Network.PlayerHitMessage.fromData(data, false); break;
			case Network.MESSAGE_ROUND_END:     message = Network.RoundEndMessage.fromData(data, false); break;
		}
		return message;
	}
}

namespace MessageHandler {
	boolean active = true;
	array listeners;
	function unregisterAllListeners() {
		.listeners = [];
	}
	function registerListener( number type, object listener ) {
		if( not .listeners.keyExists("$type") )
			.listeners["$type"] = [];
		.listeners["$type"][] = listener;
	}
	function invokeListeners( object message ) {
		if( message.type == Network.MESSAGE_DATA ) {
			object dataMessage = Network.createDataMessage(message.data);
			if( .listeners.keyExists("${dataMessage.type}") ) {
				.listeners["${dataMessage.type}"].each() using ( listener ) {
					switch( dataMessage.type ) {
						//case Network.MESSAGE_DISCONNECT_ME: listener.onDisconnectMe(dataMessage); break;
						case Network.MESSAGE_NEW_PLAYER:    listener.onNewPlayer(dataMessage); break;
						case Network.MESSAGE_PLAYER_UPDATE: listener.onPlayerUpdate(dataMessage); break;
						case Network.MESSAGE_PLAYER_HIT:    listener.onPlayerHit(dataMessage); break;
						case Network.MESSAGE_ROUND_END:     listener.onRoundEnd(dataMessage); break;
					}
				};
			}
		} else {
			if( .listeners.keyExists("${message.type}") ) {
				.listeners["${message.type}"].each() using ( listener ) {
					switch( message.type ) {
						case Network.MESSAGE_CONNECT:       listener.onConnect(message); break;
						case Network.MESSAGE_DISCONNECT_ME: listener.onDisconnectMe(message); break;
					}
				};
			}
		}
	}
	function update() {
		if( .active ) {
			object client = Network.currentClient();
			if( client and .active ) {
				object message;
				while( message = client.service(0) ) {
					.invokeListeners(message);
				}
			}
		}
	}
}
