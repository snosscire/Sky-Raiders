class KeyboardPlayerController {
	object player;
	number keyLeft;
	number keyRight;
	boolean turnLeft;
	boolean turnRight;
	constructor( object player ) {
		.player = player;
		EventHandler.registerListener(Engine.EVENT_KEYDOWN, self);
		EventHandler.registerListener(Engine.EVENT_KEYUP, self);
	}
	function update( number deltaTime ) {
		number value = .player.turningSpeed * Entity.speedMultiplier(deltaTime);
		if( .turnLeft ) {
			if( not .player.stalling ) {
				.player.angle = Engine.addToAngle(.player.angle, -value);
			}
			.player.spriteAngle = Engine.addToAngle(.player.spriteAngle, -value);
		}
		if( .turnRight ) {
			if( not .player.stalling ) {
				.player.angle = Engine.addToAngle(.player.angle, value);
			}
			.player.spriteAngle = Engine.addToAngle(.player.spriteAngle, value);
		}
	}
	function onKeyDown( object event ) {
		if( not Console.active ) {
			if( event.key == .keyLeft ) {
				.turnLeft = true;
			} else if( event.key == .keyRight ) {
				.turnRight = true;
			}
		}
	}
	function onKeyUp( object event ) {
		if( not Console.active ) {
			if( event.key == .keyLeft ) {
				.turnLeft = false;
			} else if( event.key == .keyRight ) {
				.turnRight = false;
			}
		}
	}
}
class NetworkPlayerController {
	object player;
	constructor( object player ) {
		.player = player;
		MessageHandler.registerListener(Network.MESSAGE_ENTITY_UPDATE, self);
	}
	function update() { }
	function onEntityUpdate( object message ) {
		//Engine.printLine('onEntityUpdate');
		if( not .player ) {
			Engine.printLine('Player is null');
		}
		if( not message ) {
			Engine.printLine('Message is null');
		}
		if( .player.id == message.id ) {
			.player.x = message.x;
			.player.y = message.y;
			.player.speed = message.speed;
			.player.angle = message.angle;
			.player.spriteAngle = message.spriteAngle;
			.player.health = message.health;
		}
	}
}

