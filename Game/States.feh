class State {
	boolean active;
	boolean listenersRegistered;
	function init();
	function quit();
	function update();
	function draw();
}

class TestState extends State {
	object player;
	object background;
	function init() {
		.player = new Player();
		.player.controller = new KeyboardPlayerController(.player);
		.player.controller.keyLeft = Engine.KEY_LEFT;
		.player.controller.keyRight = Engine.KEY_RIGHT;
		.player.controller.keyShoot = Engine.KEY_SPACE;
		.player.controller.keyKill = Engine.KEY_k;
		.player.weapon = new MachineGun(.player);
		.player.speed = Global.PlayerSpeed;
		.player.turningSpeed = Global.PlayerTurningSpeed;
		.player.x = Global.PlayerStartX;
		.player.y = Global.PlayerStartY;
		.background = Engine.loadImageResource('background.png');
		.active = true;
		if( not .listenersRegistered ) {
			EventHandler.registerListener(Engine.EVENT_KEYDOWN, self);
		}
	}
	function quit() {
		.active = false;
		.player = null;
		Engine.unloadImageResource(.background);
	}
	function update() {
		//number deltaTime = Time.delta;
		//while( deltaTime > 0 ) {
		//	.player.update(deltaTime);
		//	deltaTime -= 5;
		//}
		.player.update(Time.delta);
	}
	function draw() {
		Engine.drawImage(.background, 0, 0);
		.player.draw();
	}
	function onKeyDown( object event ) {
		if( .active and (not Console.active) ) {
			if( event.key == Engine.KEY_ESCAPE ) {
				Game.setState(Game.menuState());
			}
		}
	}
}

class MultiplayerState extends State {
	object client;
	boolean connected;
	array players;
	object background;
	boolean connectAutomatically;
	string host;
	number port;
	
	function setConnectAutomatically( string host, number port ) {
		.connectAutomatically = true;
		.host = host;
		.port = port;
	}
	
	function init() {
		if( not .listenersRegistered ) {
			EventHandler.registerListener(Engine.EVENT_KEYDOWN, self);
			MessageHandler.registerListener(Network.MESSAGE_DISCONNECT_ME, self);
			MessageHandler.registerListener(Network.MESSAGE_CONNECT, self);
			MessageHandler.registerListener(Network.MESSAGE_NEW_PLAYER, self);
			.listenersRegistered = true;
		}
		.background = Engine.loadImageResource('background.png');
		.connected = false;
		.active = true;
		if( .connectAutomatically ) {
			Console.printLine("------------------ connect ----------------------");
			Console.printLine("connecting to ${.host}:${.port}...");
			Console.flush();
			Network.disconnect();
			if( Network.connect(.host, .port, 'Player') ) {
				Console.printLine('waiting for response....');
			} else {
				Console.printLine('failed.');
			}
		}
	}
	function quit() {
		.active = false;
		.connected = false;
		.players = [];
		Game.players = [];
		Network.disconnect();
		Engine.unloadImageResource(.background);
	}
	function update() {
		if( .connected ) {
			//number deltaTime;
			.players.each() using ( player ) {
				player.update(Time.delta);
				//deltaTime = Time.delta;
				//Engine.printLine("Player: ${player.id}, delta time: ${deltaTime}");
				//while( deltaTime > 0 ) {
				//	player.update(deltaTime);
				//	deltaTime -= 5;
				//}
			};
		}
	}
	function draw() {
		if( .connected ) {
			Engine.drawImage(.background, 0, 0);
			.players.each() using ( player ) {
				player.draw();
			};
		}
	}
	
	function onKeyDown( object event ) {
		if( .active and (not Console.active) ) {
			if( event.key == Engine.KEY_ESCAPE ) {
				Game.setState(Game.menuState());
			}
		}
	}
	
	function createPlayer( object message, boolean me ) {
		object player;
		player = new Player();
		player.id = message.id;
		//player.name = message.name;
		player.me = me;
		player.speed = Global.PlayerSpeed;
		player.turningSpeed = Global.PlayerTurningSpeed;
		player.x = Global.PlayerStartX;
		player.y = Global.PlayerStartY;

		if( me ) {
			player.controller = new KeyboardPlayerController(player);
			player.controller.keyLeft = Engine.KEY_LEFT;
			player.controller.keyRight = Engine.KEY_RIGHT;
			player.controller.keyShoot = Engine.KEY_SPACE;
		} else {
			player.controller = new NetworkPlayerController(player);
		}
		
		player.weapon = new MachineGun(player);
		
		.players["${message.id}"] = player;
		Game.players[] = player;
	}
	
	function onConnect( object message ) {
		if( .active ) {
		}
	}
	function onDisconnectMe( object message ) {
		if( .active ) {
			Game.setState(Game.menuState());
		}
	}
	function onNewPlayer( object message ) {
		if( .active ) {
			boolean me = false;
			if( not .connected ) {
				me = true;
				.connected = true;
			}
			.createPlayer(message, me);
		}
	}
}

class MenuState extends State {
	object mainMenu;
	object multiplayerMenu;
	object currentMenu;
	function init() {
		object testState = new TestState();
		object font = Engine.loadFont('font-10.png', 10, 10);
		.mainMenu = new Menu();
		.mainMenu.setSelector('Menu/simple-selector.png');
		.mainMenu.addItem(new TextMenuItem(font, "test", closure {
			Game.setState(testState);
		}));
		.mainMenu.addItem(new TextMenuItem(font, "multiplayer", closure {
			Game.setState(Game.multiplayerState());
		}));
		.mainMenu.addItem(new TextMenuItem(font, "options"));
		.mainMenu.addItem(new TextMenuItem(font, "credits"));
		.mainMenu.addItem(new TextMenuItem(font, "quit", closure {
			Game.quit();
		}));
		.mainMenu.active = true;
		.multiplayerMenu = new Menu();
		.currentMenu = .mainMenu;
		.active = true;
		if( not .listenersRegistered ) {
			EventHandler.registerListener(Engine.EVENT_KEYDOWN, self);
			.listenersRegistered = true;
		}
	}
	function quit() {
		.active = false;
		.mainMenu = null;
		.multiplayerMenu = null;
		.currentMenu = null;
	}
	function update() {
		
	}
	function draw() {
		.currentMenu.draw();
	}
	function onKeyDown( object event ) {
		if( .active and (not Console.active) ) {
			if( event.key == Engine.KEY_ESCAPE ) {
				if( .currentMenu == .mainMenu ) {
					Game.quit();
				}
			}
		}
	}
}

