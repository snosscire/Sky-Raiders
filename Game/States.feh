class State {
	boolean active;
	boolean listenersRegistered;
	function init();
	function quit();
	function update();
	function draw();
}

class TestState extends State {
	object map;
	object playerCamera;
	object player;
	function init() {
		.map = Map.load("Test");
		
		.playerCamera = new Camera();
		
		.player = new Player();
		.player.playerSprite = new PlayerSprite(0);
		.player.controller = new KeyboardPlayerController(.player);
		.player.controller.keyLeft = Engine.KEY_LEFT;
		.player.controller.keyRight = Engine.KEY_RIGHT;
		.player.controller.keyShoot = Engine.KEY_SPACE;
		.player.controller.keyKill = Engine.KEY_k;
		.player.weapon = new MachineGun(.player);
		.player.speed = Global.PlayerSpeed;
		.player.turningSpeed = Global.PlayerTurningSpeed;
		.player.x = Global.PlayerStartX;
		.player.y = Global.PlayerStartY;
		
		.active = true;
		
		if( not .listenersRegistered ) {
			EventHandler.registerListener(Engine.EVENT_KEYDOWN, self);
		}
	}
	function quit() {
		.active = false;
		.player = null;
		.map.unload();
		.map = null;
	}
	function update() {
		//number deltaTime = Time.delta;
		//while( deltaTime > 0 ) {
		//	.player.update(deltaTime);
		//	deltaTime -= 5;
		//}
		.player.update(Time.delta, .map);
		.playerCamera.update(.map, .player);
		.map.update(.playerCamera);
	}
	function draw() {
		.map.drawBackground(.playerCamera);
		.player.draw(.playerCamera, .map);
		.map.drawForeground(.playerCamera);
	}
	function onKeyDown( object event ) {
		if( .active and (not Console.active) ) {
			if( event.key == Engine.KEY_ESCAPE ) {
				Game.setState(Game.menuState());
			}
		}
	}
}

class MultiplayerState extends State {
	object client;
	boolean connected;
	object currentMap;
	object playerCamera;
	object localPlayer;
	array players;
	boolean connectAutomatically;
	string host;
	number port;
	object scoreScreen;
	boolean showScoreScreen;
	boolean askAreYouSure;
	boolean paused;
	number connectionInitiatedTime;
	string localPlayerName = "player";
	
	function setConnectAutomatically( string host, number port ) {
		.connectAutomatically = true;
		.host = host;
		.port = port;
	}
	
	function init() {
		if( not .listenersRegistered ) {
			EventHandler.registerListener(Engine.EVENT_KEYDOWN, self);
			EventHandler.registerListener(Engine.EVENT_KEYUP, self);
			MessageHandler.registerListener(Network.MESSAGE_DISCONNECT_ME, self);
			MessageHandler.registerListener(Network.MESSAGE_CONNECT, self);
			MessageHandler.registerListener(Network.MESSAGE_NEW_PLAYER, self);
			MessageHandler.registerListener(Network.MESSAGE_PLAYER_HIT, self);
			MessageHandler.registerListener(Network.MESSAGE_ROUND_END, self);
			MessageHandler.registerListener(Network.MESSAGE_GAME_RESTART, self);
			MessageHandler.registerListener(Network.MESSAGE_PLAYER_DISCONNECT, self);
			.listenersRegistered = true;
		}
		//.currentMap = Map.load("Test");
		.playerCamera = new Camera();
		.showScoreScreen = false;
		.scoreScreen = new ScoreScreen();
		.askAreYouSure = false;
		.connected = false;
		.paused = false;
		.active = true;
		if( .connectAutomatically ) {
			Console.printLine("------------------ connect ----------------------");
			Console.printLine("connecting to ${.host}:${.port}...");
			Console.flush();
			Network.disconnect();
			if( Network.connect(.host, .port, 'Player') ) {
				Console.printLine('waiting for response....');
			} else {
				Console.printLine('failed.');
			}
		}
	}
	function quit() {
		.showScoreScreen = false;
		.scoreScreen = null;
		.askAreYouSure = false;
		.paused = false;
		.active = false;
		.connected = false;
		.currentMap and .currentMap.unload();
		.currentMap = null;
		.playerCamera = null;
		.localPlayer and Network.send(new Network.PlayerDisconnectMessage(.localPlayer));
		.localPlayer = null;
		.players = [];
		Game.players = [];
		Network.flush();
		Network.disconnect();
	}
	function update() {
		if( .connected and .paused == false ) {
			//number deltaTime;
			.players.each() using ( player ) {
				//player.me and Console.printLine('updating me...');
				player.update(Time.delta, .currentMap);
				//deltaTime = Time.delta;
				//Engine.printLine("Player: ${player.id}, delta time: ${deltaTime}");
				//while( deltaTime > 0 ) {
				//	player.update(deltaTime);
				//	deltaTime -= 5;
				//}
			};
			//.localPlayer.update(Time.delta, .currentMap);
			.playerCamera.update(.currentMap, .localPlayer);
			.currentMap.update(.playerCamera);
		}
	}
	function draw() {
		if( .connected ) {
			.currentMap.drawBackground(.playerCamera);
			.players.each() using ( player ) {
				player.draw(.playerCamera, .currentMap, (.showScoreScreen or .askAreYouSure ? false : true));
			};
			//.localPlayer.draw(.playerCamera, .currentMap);
			.currentMap.drawForeground(.playerCamera);
			if( .askAreYouSure ) {
				object font = Engine.loadFont("font-menu.png", 20, 20);
				string text = "are you sure you want to quit... y/n";
				number x = (Engine.screenWidth() / 2) - (font.textWidth(text) / 2);
				number y = (Engine.screenHeight() / 2) - (font.height / 2);
				font.drawText(text, x, y);
			} else if( .showScoreScreen ) {
				.scoreScreen and .scoreScreen.draw();
			}
		}
	}
	
	function onKeyDown( object event ) {
		if( .active and (not Console.active) ) {
			if( event.key == Engine.KEY_ESCAPE ) {
				if( .connected ) {
					.askAreYouSure = true;
				} else {
					Game.setState(Game.menuState());
				}
			} else if( event.key == Engine.KEY_TAB ) {
				.showScoreScreen = true;
			}
			if( .askAreYouSure ) {
				if( event.key == Engine.KEY_y ) {
					Game.setState(Game.menuState());
				} else if( event.key == Engine.KEY_n ) {
					.askAreYouSure = false;
				}
			}
		}
	}
	function onKeyUp( object event ) {
		if( .active and (not Console.active) ) {
			if( event.key == Engine.KEY_TAB ) {
				.showScoreScreen = false;
			}
		}
	}
	
	function createPlayer( object message, boolean me ) {
		object player;
		
		if( .players.keyExists("Player/${message.id}") )
			return; // This should not be possible but for some reason this happens sometimes.
		
		Console.printLine("creating player... ${me} (${message.id})");
		
		player = new Player();
		player.id = message.id;
		player.name = message.name;
		player.me = me;
		player.health = Global.PlayerStartHealth;
		player.speed = Global.PlayerSpeed;
		player.turningSpeed = Global.PlayerTurningSpeed;
		player.x = message.x;
		player.y = message.y;
		player.spawnLocationX = message.x;
		player.spawnLocationY = message.y;
		player.color = message.color;
		player.playerSprite = new PlayerSprite(player.color);
		
		if( me ) {
			//Console.printLine('using keyboard controller.');
			player.controller = new KeyboardPlayerController(player);
			player.controller.keyLeft = Engine.KEY_LEFT;
			player.controller.keyRight = Engine.KEY_RIGHT;
			player.controller.keyShoot = Engine.KEY_SPACE;
			.localPlayer = player;
		} else {
			player.controller = new NetworkPlayerController(player);
		}
		
		player.weapon = new MachineGun(player);
		
		.players["Player/${player.id}"] = player;
		Game.players["Player/${player.id}"] = player;
	}
	
	function onConnect( object message ) {
		if( .active ) {
			Network.send(new Network.InitializeJoinMessage(.localPlayerName));
		}
	}
	function onDisconnectMe( object message ) {
		if( .active ) {
			Game.setState(Game.menuState());
		}
	}
	function onPlayerDisconnect( object message ) {
		number index;
		object player;
		for( number i = 0; i < .players.size(); i++ ) {
			if( .players[i].id == message.id  ) {
				index = i;
				player = .players[i];
			}
		}
		Console.printLine("${player.name} disconnected.");
		.players.deleteAt(index);
		Game.players.deleteAt(index);
	}
	function onNewPlayer( object message ) {
		if( .active ) {
			boolean me = false;
			if( not .connected ) {
				me = true;
				.connected = true;
				.currentMap = Map.load(message.map);
			}
			.createPlayer(message, me);
		}
	}
	function onPlayerHit( object message ) {
		//if( .players.keyExists("Player/${message.shooter}") and .players.keyExists("Player/${message.id}") ) {
			object shooter = .players["Player/${message.shooter}"];
			object player = .players["Player/${message.id}"];
			player.health = message.health;
			player.reduceHealth(message.damage);
			if( player.health > 0 ) {
				Console.printLine("${shooter.name} hit ${player.name} for ${message.damage} damage.");
			} else {
				Console.printLine("${shooter.name} killed ${player.name}.");
				.players.keyExists("Player/${shooter.id}") and .players["Player/${shooter.id}"].score++;
			}
		//}
	}
	
	function onRoundEnd( object message ) {
		.scoreScreen.setWinner(.players["Player/${message.winner}"].name);
		.showScoreScreen = true;
		.paused = true;
	}
	function onGameRestart( object message ) {
		.players.each() using ( player ) {
			player.x = player.spawnLocationX;
			player.y = player.spawnLocationY;
			player.angle = Global.PlayerStartAngle;
			player.spriteAngle = Global.PlayerStartAngle;
			player.speed = Global.PlayerSpeed;
			player.turningSpeed = Global.PlayerTurningSpeed;
			player.health = Global.PlayerStartHealth;
			player.score = 0;
		};
		.paused = false;
		.showScoreScreen = false;
		.scoreScreen.roundEnd = false;
	}
}

class OldMenuState extends State {
	object mainMenu;
	object multiplayerMenu;
	object currentMenu;
	function init() {
		object testState = new TestState();
		object font = Engine.loadFont('font-10.png', 10, 10);
		.mainMenu = new Menu();
		.mainMenu.setSelector('Menu/simple-selector.png');
		.mainMenu.addItem(new TextMenuItem(font, "test", closure {
			Game.setState(testState);
		}));
		.mainMenu.addItem(new TextMenuItem(font, "multiplayer", closure {
			Game.setState(Game.multiplayerState());
		}));
		.mainMenu.addItem(new TextMenuItem(font, "options"));
		.mainMenu.addItem(new TextMenuItem(font, "credits"));
		.mainMenu.addItem(new TextMenuItem(font, "quit", closure {
			Game.quit();
		}));
		.mainMenu.active = true;
		.multiplayerMenu = new Menu();
		.currentMenu = .mainMenu;
		.active = true;
		if( not .listenersRegistered ) {
			EventHandler.registerListener(Engine.EVENT_KEYDOWN, self);
			.listenersRegistered = true;
		}
	}
	function quit() {
		.active = false;
		.mainMenu = null;
		.multiplayerMenu = null;
		.currentMenu = null;
	}
	function update() {
		
	}
	function draw() {
		.currentMenu.draw();
	}
	function onKeyDown( object event ) {
		if( .active and (not Console.active) ) {
			if( event.key == Engine.KEY_ESCAPE ) {
				if( .currentMenu == .mainMenu ) {
					Game.quit();
				}
			}
		}
	}
}

class MenuState extends State {
	object mainMenu;
	object startMenu;
	object joinMenu;
	object settingsMenu;
	object currentMenu;
	boolean menusInited = false;
	function init() {
		if( not .menusInited ) {
			.mainMenu = new ImageMenu();
			.mainMenu.setImage("main_menu_indexed.png");
			.mainMenu.addInvisibleButton("StartButton", 500, 108, 250, 100) using {
				.currentMenu.setActive(false);
				.startMenu.setActive(true);
				.currentMenu = .startMenu;
			};
			.mainMenu.addInvisibleButton("JoinButton", 500, 241, 250, 100) using {
				.currentMenu.setActive(false);
				.joinMenu.setActive(true);
				.currentMenu = .joinMenu;
			};
			.mainMenu.addInvisibleButton("SettingsButton", 500, 374, 250, 100) using {
				.currentMenu.setActive(false);
				.settingsMenu.setActive(true);
				.currentMenu = .settingsMenu;
			};
			.mainMenu.addInvisibleButton("QuitButton", 500, 507, 250, 100) using {
				Game.quit();
			};
		
			.startMenu = new ImageMenu();
			.startMenu.setImage("create_menu_indexed.png");

			.joinMenu = new ImageMenu();
			.joinMenu.setImage("join_menu_indexed.png");
			.joinMenu.addInvisibleTextfield("PlayerNameTextfield", 567, 301, 309, 27, 15);
			.joinMenu.addInvisibleTextfield("HostTextfield", 567, 373, 309, 27, 15);
			.joinMenu.addInvisibleTextfield("PortTextfield", 567, 446, 309, 27, 15);
			.joinMenu.getItem("PortTextfield").text = '' + Global.ServerDefaultPort;
			.joinMenu.addInvisibleButton("BackButton", 212, 562, 250, 100) using {
				.currentMenu.setActive(false);
				.mainMenu.setActive(true);
				.currentMenu = .mainMenu;
			};
			.joinMenu.addInvisibleButton("JoinButton", 817, 562, 250, 100) using {
				string playerName = .joinMenu.getItem("PlayerNameTextfield").text;
				string host = .joinMenu.getItem("HostTextfield").text;
				number port = .joinMenu.getItem("PortTextfield").text.toNumber();
				if( playerName and host and port ) {
					boolean connected = false;
					Console.printLine("------------------ connect ----------------------");
					Console.printLine("connecting to ${host}:${port}...");
					Console.flush();
					Network.disconnect();
					connected = Network.connect(host, port, 'Player');
					if( connected ) {
						object multiplayerState = Game.multiplayerState();
						Console.printLine('waiting for response...');
						multiplayerState.connectionInitiatedTime = Time.current;
						multiplayerState.localPlayerName = playerName;
						Game.setState(multiplayerState);
					} else {
						Console.printLine('failed.');
					}
				}
			};
			
			.settingsMenu = new ImageMenu();
			.settingsMenu.setImage("settings_menu_indexed.png");
			// show fps
			.settingsMenu.addInvisibleCheckBox("ShowFpsCheckBox", 547, 459, 32, 29, false) using {
				// Do nothing
			};
			// low quality
			.settingsMenu.addInvisibleCheckBox("LowQualityCheckBox", 915, 459, 32, 29, false) using {
				Console.printLine('click');
			};
			// windowed mode
			.settingsMenu.addInvisibleCheckBox("WindowedModeCheckBox", 675, 520, 32, 29, (not Global.DefaultFullScreenState)) using {
				Console.printLine('click');
			};
			// back
			.settingsMenu.addInvisibleButton("BackButton", 212, 585, 250, 100) using {
				.currentMenu.setActive(false);
				.mainMenu.setActive(true);
				.currentMenu = .mainMenu;
			};
			// ok
			.settingsMenu.addInvisibleButton("OkButton", 817, 585, 250, 100) using {
				number newScreenWidth = 1024;
				number newScreenHeight = 768;
				boolean fullscreen = (not .settingsMenu.getItem("WindowedModeCheckBox").checked);
				string selectedResolution = .settingsMenu.getItem("ScreenResolutionSlider").selectedOption;
				if( selectedResolution == "1280x720" ) {
					newScreenWidth = 1280;
					newScreenHeight = 720;
				} else if( selectedResolution == "1280x800" ) {
					newScreenWidth = 1280;
					newScreenHeight = 800;
				} else if ( selectedResolution == "1280x1024" ) {
					newScreenWidth = 1280;
					newScreenHeight = 1024;
				}
				Engine.setScreen(newScreenWidth, newScreenHeight, fullscreen);
				Game.showFPS = .settingsMenu.getItem("ShowFpsCheckBox").checked;
				.currentMenu.setActive(false);
				.mainMenu.setActive(true);
				.currentMenu = .mainMenu;
			};
			// aspect ratio
			.settingsMenu.addInvisibleSlider("AspectRatioSlider", 275, 304, 730, 27, [ "4:3", "16:9", "16:10" ]) using ( string item ) {
				if( item == "4:3" ) {
					.settingsMenu.getItem("AspectRatioLabel").setImage("GUI/4_3.png");
					.settingsMenu.getItem("ScreenResolutionSlider").options = [ "1024x768", "1280x1024" ];
					.settingsMenu.getItem("ScreenResolutionSlider").selectedOption = "1024x768";
					.settingsMenu.getItem("ResolutionLabel").setImage("GUI/1024_768.png");
				} else if( item == "16:10" ) {
					.settingsMenu.getItem("AspectRatioLabel").setImage("GUI/16_10.png");
					.settingsMenu.getItem("ScreenResolutionSlider").options = [ "1280x800" ];
					.settingsMenu.getItem("ScreenResolutionSlider").selectedOption = "1280x800";
					.settingsMenu.getItem("ResolutionLabel").setImage("GUI/1280_800.png");
				} else if( item == "16:9" ) {
					.settingsMenu.getItem("AspectRatioLabel").setImage("GUI/16_9.png");
					.settingsMenu.getItem("ScreenResolutionSlider").options = [ "1280x720" ];
					.settingsMenu.getItem("ScreenResolutionSlider").selectedOption = "1280x720";
					.settingsMenu.getItem("ResolutionLabel").setImage("GUI/1280_720.png");
				}
			};
			// resolution
			.settingsMenu.addInvisibleSlider("ScreenResolutionSlider", 275, 398, 730, 27, [ "1024x768", "1280x1024" ]) using ( string item ) {
				if( item == "1024x768" ) .settingsMenu.getItem("ResolutionLabel").setImage("GUI/1024_768.png");
				else if( item == "1280x720" ) .settingsMenu.getItem("ResolutionLabel").setImage("GUI/1280_720.png");
				else if( item == "1280x800" ) .settingsMenu.getItem("ResolutionLabel").setImage("GUI/1280_800.png");
				else if( item == "1280x1024" ) .settingsMenu.getItem("ResolutionLabel").setImage("GUI/1280_1024.png");
			};
			.settingsMenu.getItem("ScreenResolutionSlider").selectedOption = "1024x768";
			// aspect ratio text
			.settingsMenu.addImage("AspectRatioLabel", 600, 253);
			.settingsMenu.getItem("AspectRatioLabel").setImage("GUI/4_3.png");
			// resolution text
			.settingsMenu.addImage("ResolutionLabel", 600, 347);
			.settingsMenu.getItem("ResolutionLabel").setImage("GUI/1024_768.png");
			
			.menusInited = true;
		}
		
		.currentMenu and .currentMenu.setActive(false);
		.currentMenu = .mainMenu;
		.currentMenu.setActive(true);
		
		.active = true;
		
		if( not .listenersRegistered ) {
			EventHandler.registerListener(Engine.EVENT_KEYDOWN, self);
			.mainMenu.registerListeners();
			.startMenu.registerListeners();
			.joinMenu.registerListeners();
			.settingsMenu.registerListeners();
			.listenersRegistered = true;
		}
	}
	function quit() {
		.active = false;
	}
	function update() {
	
	}
	function draw() {
		.currentMenu and .currentMenu.draw();
	}
	function onKeyDown( object event ) {
		if( .active and (not Console.active) ) {
			if( event.key == Engine.KEY_ESCAPE ) {
				if( .currentMenu == .mainMenu ) {
					Game.quit();
				} else {
					.currentMenu.setActive(false);
					.mainMenu.setActive(true);
					.currentMenu = .mainMenu;
				}
			}
		}
	}
}

