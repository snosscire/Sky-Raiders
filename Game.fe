uses 'Engine/Engine.feh';
uses 'Game/Events.feh';
uses 'Game/Entities.feh';
uses 'Game/Controllers.feh';

class Game {
	static final number GRAVITY = 1.0;
	static boolean running;
	static function run() {
		if( not .running ) {
			object this = new Game();
			object player;
			object playerController;
			object background;
			
			number frames = 0;
			number fps = 0;
			
			Engine.resourcePath = './Resources/';
			Engine.imageResourcePath = Engine.resourcePath + 'Images/';
			
			Engine.setScreen(640, 480, false);
			
			EventHandler.unregisterAllListeners();
			EventHandler.registerListener(Engine.EVENT_QUIT, this);
			EventHandler.registerListener(Engine.EVENT_KEYDOWN, this);
			EventHandler.registerListener(Engine.EVENT_KEYUP, this);
			
			playerController = new KeyboardController();
			playerController.keyLeft = Engine.KEY_LEFT;
			playerController.keyRight = Engine.KEY_RIGHT;
			player = new Player();
			player.controller = playerController;
			player.speed = 1.8;
			player.turningSpeed = 1.0;
			player.x = 200;
			player.y = 100;
			
			Time.current = Engine.currentTime();
			Time.last = Time.current;
			Time.delta = 0;
			Time.speedMultiplier = 0.0;
			
			background = Engine.loadImageResource('background.png');
			
			.running = true;
			while( .running ) {
				//frames++;
				//fps = frames / (Engine.currentTime() / 1000.0);
				//Engine.printLine('FPS: ' + fps);
				//Engine.printLine('Current time: ' + Engine.currentTime());
				Time.current = Engine.currentTime();
				Time.delta = Time.current - Time.last;
				Time.last = Time.current;
				Time.speedMultiplier = Time.delta / 10.0;
				//Engine.printLine('Delta time: ' + Time.delta);
				EventHandler.update();
				player.update();
				Engine.clearScreen();
				Engine.drawImage(background, 0, 0);
				player.draw();
				Engine.updateScreen();
			}
			
			background.unload();
		}
	}
	static function quit() {
		.running = false;
	}
	function onQuit( object event ) {
		Game.quit();
	}
	function onKeyDown( object event ) {
		Engine.printLine('Key down: ' + event.key);
		if( event.key == Engine.KEY_ESCAPE )
			Game.quit();
	}
	function onKeyUp( object event ) {
		Engine.printLine('Key up: ' + event.key);
	}
}

Engine.printLine("Hello World!");
Game.run();

